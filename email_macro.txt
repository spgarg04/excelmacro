Sub Main_Function()
'
' Macro1 Macro
'
    Dim ws1, ws2, ws3 As Worksheet
    Dim LastRow, LastRow2 As Long
    Dim email_id As String
    Set ws1 = Worksheets("Temp")
    With ws1
        LastRow = .Cells(.Rows.Count, "A").End(xlUp).Row
    End With
    'MsgBox LastRow
    
    Set ws2 = Worksheets("Data")
    Set ws3 = Worksheets("Sheet4")
    For i = 2 To LastRow
        email_id = ws1.Range("a" & i).Value
        ws2.Range("A3").AutoFilter Field:=15, Criteria1:=email_id
        'ws2.Range("A:O").SpecialCells(xlCellTypeVisible).Copy
        'ws3.Range("a1").PasteSpecial xlPasteAll
        Mail_Selection_Range_Outlook_Body (email_id)
    Next i
    
End Sub
Sub RemoveEmailDuplicates()
'
' Macro2 Macro
'
    Dim ws1, ws2 As Worksheet
    Set ws1 = Worksheets("Data")
    Set ws2 = Worksheets("Temp")
    ws1.Range("a1:o10000").AutoFilter
    ws1.Range("O1:O10000").Copy
    ws2.Range("a1").PasteSpecial xlPasteAll
    ws2.Range("A1:A10000").RemoveDuplicates Columns:=1, Header:=xlYes
   
End Sub

Sub Mail_Selection_Range_Outlook_Body(email_id As String)
'For Tips see: http://www.rondebruin.nl/win/winmail/Outlook/tips.htm
'Don't forget to copy the function RangetoHTML in the module.
'Working in Excel 2000-2016
    Dim rng As Range
    Dim OutApp As Object
    Dim OutMail As Object
    Dim ws2 As Worksheet
    
    Set ws2 = Worksheets("Data")

    Set rng = Nothing
    On Error Resume Next
    'Only the visible cells in the selection
    Set rng = ws2.Range("A:O").SpecialCells(xlCellTypeVisible)
    'You can also use a fixed range if you want
    'Set rng = Sheets("YourSheet").Range("D4:D12").SpecialCells(xlCellTypeVisible)
    On Error GoTo 0

    If rng Is Nothing Then
        MsgBox "The selection is not a range or the sheet is protected" & _
               vbNewLine & "please correct and try again.", vbOKOnly
        Exit Sub
    End If

    With Application
        .EnableEvents = False
        .ScreenUpdating = False
    End With

    Set OutApp = CreateObject("Outlook.Application")
    Set OutMail = OutApp.CreateItem(0)

    On Error Resume Next
    With OutMail
        .To = email_id
        .CC = ""
        .BCC = ""
        .Subject = "This is the Subject line"
        .HTMLBody = RangetoHTML(rng)
        .Display    'or use .Display
    End With
    On Error GoTo 0

    With Application
        .EnableEvents = True
        .ScreenUpdating = True
    End With

    Set OutMail = Nothing
    Set OutApp = Nothing
End Sub


Function RangetoHTML(rng As Range)
' Changed by Ron de Bruin 28-Oct-2006
' Working in Office 2000-2016
    Dim fso As Object
    Dim ts As Object
    Dim TempFile As String
    Dim TempWB As Workbook

    TempFile = Environ$("temp") & "\" & Format(Now, "dd-mm-yy h-mm-ss") & ".htm"

    'Copy the range and create a new workbook to past the data in
    rng.Copy
    Set TempWB = Workbooks.Add(1)
    With TempWB.Sheets(1)
        .Cells(1).PasteSpecial Paste:=8
        .Cells(1).PasteSpecial xlPasteValues, , False, False
        .Cells(1).PasteSpecial xlPasteFormats, , False, False
        .Cells(1).Select
        Application.CutCopyMode = False
        On Error Resume Next
        .DrawingObjects.Visible = True
        .DrawingObjects.Delete
        On Error GoTo 0
    End With

    'Publish the sheet to a htm file
    With TempWB.PublishObjects.Add( _
         SourceType:=xlSourceRange, _
         Filename:=TempFile, _
         Sheet:=TempWB.Sheets(1).Name, _
         Source:=TempWB.Sheets(1).UsedRange.Address, _
         HtmlType:=xlHtmlStatic)
        .Publish (True)
    End With

    'Read all data from the htm file into RangetoHTML
    Set fso = CreateObject("Scripting.FileSystemObject")
    Set ts = fso.GetFile(TempFile).OpenAsTextStream(1, -2)
    RangetoHTML = ts.readall
    ts.Close
    RangetoHTML = Replace(RangetoHTML, "align=center x:publishsource=", _
                          "align=left x:publishsource=")

    'Close TempWB
    TempWB.Close savechanges:=False

    'Delete the htm file we used in this function
    Kill TempFile

    Set ts = Nothing
    Set fso = Nothing
    Set TempWB = Nothing
End Function

